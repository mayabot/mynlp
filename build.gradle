/*
 * Copyright 2018 mayabot.com authors. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'idea'
apply plugin: 'eclipse'


ext {
    guava_version = "19.0"
    mynlp_version = "1.0.0-snapshot"
}


group = 'com.mayabot.mynlp'
version = "$mynlp_version"
description = "mynlp"


subprojects {
    description = "Maya Nlp subproject ${project.path}"

    group = 'com.mayabot.mynlp'
    version = "$mynlp_version"

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier "javadoc"
        from javadoc.destinationDir
    }

    sourceSets {
        example {
            compileClasspath += sourceSets.main.runtimeClasspath
            runtimeClasspath += sourceSets.main.runtimeClasspath
        }
    }

    project.plugins.withType(JavaBasePlugin) {
        publishing {
            publications {
                java(MavenPublication) {
                    from components.java

                    artifact sourceJar {
                        classifier "sources"
                    }

                    artifact javadocJar {
                        classifier "javadoc"
                    }
                }
            }
        }
    }

    project.afterEvaluate {
        if (plugins.hasPlugin('java')) {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
        }
    }
}


idea {
    project {
        vcs = 'Git'
    }
}


repositories {
    mavenCentral()
}


if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

